apply plugin: 'com.android.library'

repositories {
    jcenter()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23

        externalNativeBuild {
            ndkBuild {
                arguments '-C', 'src/main', '-j', Runtime.runtime.availableProcessors() + ''
                abiFilters "armeabi-v7a"
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

// by default recent plugin version does not copy any AIDL files "to avoid publishing too much"
android.libraryVariants.all { variant ->
    Sync packageAidl = project.tasks.create("addPublic${variant.name.capitalize()}Aidl", Sync) { sync ->
        from "$project.projectDir/src/main/aidl/"
        into "$buildDir/intermediates/bundles/${variant.dirName}/aidl/"
    }

    variant.javaCompile.dependsOn packageAidl
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:25.1.0'
}
